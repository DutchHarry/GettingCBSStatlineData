USE tempdb;
GO

CREATE TABLE tmp_OneColumn_Head (
  OneColumn varchar(max) NULL
);
GO
--DROP TABLE tmp_OneColumn_Head;
GO

TRUNCATE TABLE [tmp_OneColumn_Head];
GO
BULK INSERT [tmp_OneColumn_Head]
FROM 'G:\_downloads\CBS Statline\83670NED_UntypedDataSet_05032020_101641.csv'
WITH (
  TABLOCK 
, CODEPAGE = 'RAW'
, DATAFILETYPE = 'char'
, FIRSTROW = 1
, LASTROW = 1
, ROWTERMINATOR = '\r' 
);
GO
--SELECT [OneColumn] FROM [tmp_OneColumn_Head];

/**
Create the table for the data from the column names in the first row
**/
DECLARE @crlf char(2) = CHAR(13)+CHAR(10);
DECLARE @sqlcolumns varchar(max);
DECLARE @sql nvarchar(max);
DECLARE @delimiter varchar(4) = ';';  --this file has ';' as delimiter, but please check for your cases
SELECT @sqlcolumns = [OneColumn] FROM [tmp_OneColumn_Head];
SET @sqlcolumns = '  [' + REPLACE(REPLACE(@sqlcolumns,'"',''),@delimiter,'] [varchar](8000) NULL' + @crlf + ', [') + '] [varchar](8000) NULL';
--PRINT @sqlcolumns;
SET @sql = 'CREATE TABLE [tmp_MultiColumn_Data] (' + @crlf + @sqlcolumns + @crlf + ');';
--PRINT @sql;
EXECUTE (@sql);
--EXECUTE sp_executesql  @sql;
GO
--DROP TABLE [tmp_MultiColumn_Data];
GO

TRUNCATE TABLE [tmp_MultiColumn_Data];
GO
BULK INSERT [tmp_MultiColumn_Data]
FROM 'G:\_downloads\CBS Statline\83670NED_UntypedDataSet_05032020_101641.csv'
WITH (
  TABLOCK 
, CODEPAGE = 'RAW'
, DATAFILETYPE = 'char'
, FIRSTROW = 2
, FIELDTERMINATOR = ';'
, ROWTERMINATOR = '\n' 
, FIELDQUOTE = '"' --https://tools.ietf.org/html/rfc4180 
, FORMAT = 'CSV'  --this only works in SQL2017 and higher, for earlier versionsyou'll have to strip double quotes from the columns
);
GO
SELECT * FROM [tmp_MultiColumn_Data];
GO
DROP TABLE [tmp_MultiColumn_Data];
GO

/**
that's it
you data is in a SQL server table for further processing
**/

